#!/bin/zsh
#
# zsh_prompt
# zsh-specific prompting
autoload -Uz promptinit
promptinit
colors

# VCS information
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable svn git bzr darcs hg
zstyle ':vcs_info:*' actionformats "[%s:%b|%a]"
zstyle ':vcs_info:*' formats       "[%s:%b]"

# Helper definitions
PS_ARROW=">"
if has_unicode; then
	PS_ARROW="Â»"
fi
PS_WINDOW=""
if [[ -n "$WINDOW" ]]; then
	PS_WINDOW='#$WINDOW:'
fi
PS_STATUS='%B%(?..[%?] )%b'
PS_PWD='%{${fg_no_bold[blue]}%}%55<...<%~%{${reset_color}%}'
PS_VCS='%{${fg_no_bold[magenta]}%}${vcs_info_msg_0_}%{${reset_color}%}'

# Set the prompt
PROMPT="${PS_STATUS}${PS_PWD}"
if ! is_login; then
	PROMPT="[${PROMPT}]"
fi
PROMPT="${PROMPT} ${PS_ARROW} "
RPROMPT="${PS_VCS}"
if [[ -n "$SSH_CLIENT" ]]; then
	RPROMPT="$RPROMPT|%n@%m"
fi

# Set the title
precmd () {
	vcs_info
	title "$is" "%n@%m" "%55<...<%~"
}

# Set the title for external commands
preexec () {
	title "$1" "%n@%m" "%35<...<%~"
}
